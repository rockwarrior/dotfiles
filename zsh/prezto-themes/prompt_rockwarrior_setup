#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_mshaffer_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="[${solar_brblue}%b%f%u%c${solar_green}●%f]"
  else
    branch_format="[${solar_brblue}%b%f%u%c]"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_mshaffer_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_mshaffer_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    solar_black="%F{235}"
    solar_white="%F{254}"
    solar_yellow="%F{136}"
    solar_red="%F{160}"
    solar_magenta="%F{125}"
    solar_pink="%F{161}"
    solar_purple="%F{165}"
    solar_blue="%F{33}"
    solar_cyan="%F{37}"
    solar_green="%F{64}"
    solar_brblue="%F{244}"
  else
    solar_black="%F{black}"
    solar_white="%F{white}"
    solar_yellow="%F{yellow}"
    solar_red="%F{red}"
    solar_magenta="%F{magenta}"
    solar_pink="%F{magenta}"
    solar_purple="%F{magenta}"
    solar_blue="%F{blue}"
    solar_cyan="%F{cyan}"
    solar_green="%F{green}"
    solar_brblue="%F{brblue}"
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${solar_cyan}%b%f%u%c)"
  local action_format="(${solar_green}%a%f)"
  local unstaged_format="${solar_pink}●%f"
  local staged_format="${solar_purple}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  # Define prompts.
  PROMPT="
${solar_cyan}%n:%f ${solar_yellow}%~%f "'${vcs_info_msg_0_}'"
"'$python_info[virtualenv]'"$ "
  RPROMPT=''
}

prompt_mshaffer_setup "$@"

